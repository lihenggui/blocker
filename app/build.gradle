apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 32
    defaultConfig {
        applicationId "com.merxury.blocker"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 1264
        versionName "1.2.65-alpha"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs 'en', 'zh-rCN', 'zh-rTW', 'ru'
        multiDexEnabled true
        buildConfigField "String", "GitHash", "\"${getGitHash()}\""
    }

    buildFeatures {
        viewBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    signingConfigs {

    }

    buildTypes {
        debug {
            debuggable true
        }
        release {
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"

    productFlavors {
        basic {
            ext.enableCrashlytics = false
        }
        prod {
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }
    namespace 'com.merxury.blocker'
}

// Rename generated APKs
android.applicationVariants.all { variant ->
    def flavor = variant.productFlavors[0].name
    def archiveName = "Blocker-${variant.versionName}-${flavor}-${getGitHash()}"
    variant.outputs.all {
        outputFileName = "${archiveName}.apk"
    }
}

kapt {
    correctErrorTypes true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.annotation:annotation:1.4.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.browser:browser:1.4.0'
    implementation "androidx.cardview:cardview:1.0.0"
    // Android WorkManager
    implementation("androidx.work:work-runtime:2.7.1")
    implementation("androidx.work:work-runtime-ktx:2.7.1")
    // Android navigation
    implementation "androidx.navigation:navigation-fragment-ktx:2.5.0"
    implementation "androidx.navigation:navigation-ui-ktx:2.5.0"
    implementation "androidx.navigation:navigation-compose:2.5.0"
    // Jetpack compose
    implementation 'androidx.activity:activity-compose:1.5.0'
    implementation 'androidx.compose.material:material:1.1.1'
    implementation 'androidx.compose.animation:animation:1.1.1'
    implementation 'androidx.compose.ui:ui-tooling:1.1.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.0'
    implementation "com.google.android.material:compose-theme-adapter:1.1.14"
    // Rooms
    implementation "androidx.room:room-runtime:2.4.2"
    implementation("androidx.room:room-ktx:2.4.2")
    kapt "androidx.room:room-compiler:2.4.2"
    // KTX
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.0'
    // Crashlytics
    prodImplementation platform('com.google.firebase:firebase-bom:29.2.0')
    prodImplementation 'com.google.firebase:firebase-crashlytics-ktx'
    prodImplementation 'com.google.firebase:firebase-analytics-ktx'
    // Hilt
    implementation "com.google.dagger:hilt-android:2.42"
    kapt "com.google.dagger:hilt-android-compiler:2.42"
    // Project dependencies
    implementation project(':core')
    implementation project(':ifw-api')
    implementation project(':libkit')
    // 3rd party dependencies
    implementation 'me.zhanghai.android.appiconloader:appiconloader:1.4.0'
    implementation 'com.airbnb.android:lottie:5.2.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.github.bumptech.glide:glide:4.13.0'
    kapt 'com.github.bumptech.glide:compiler:4.13.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
